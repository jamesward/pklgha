module pklgha.GitHubActionJava

import "GitHubAction.pkl"

function setupJava(distribution: String?, javaVersion: String?, cache: String?): GitHubAction.Step = new {
  uses = "actions/setup-java@v5"
  with {
    ["distribution"] = distribution ?? "temurin"
    ["java-version"] = javaVersion ?? "21"
    ["cache"] = cache
  }
}

function gradleSteps(taskString: String(!isEmpty), workingDirectory: String?): Listing<GitHubAction.Step> = new {
  GitHubAction.stepCheckout

  setupJava(null, null, "gradle")

  new {
    uses = "gradle/actions/setup-gradle@v5"
  }

  new {
    run = "./gradlew \(taskString)"
    `working-directory` = workingDirectory
  }
}

function gradleTestSteps(taskString: String(!isEmpty)?, workingDirectory: String?): Listing<GitHubAction.Step> = new {
  ...gradleSteps(taskString ?? "test", workingDirectory)
}

function mavenTestSteps(taskString: String(!isEmpty)?, workingDirectory: String?, distribution: String?, javaVersion: String?): Listing<GitHubAction.Step> = new {
  GitHubAction.stepCheckout

  setupJava(distribution, javaVersion, "maven")

  new {
    run = "./mvnw \(taskString ?? "test")"
    `working-directory` = workingDirectory
  }
}
